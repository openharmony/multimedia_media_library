# Copyright (C) 2026 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/multimedia/media_library/media_library.gni")

config("media_library_camera_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "${MEDIALIB_INTERFACES_PATH}/inner_api/media_library_camera_helper/include",
    "${MEDIALIB_INTERFACES_PATH}/inner_api/media_library_camera_helper/include/enum",
    "${MEDIALIB_INTERFACES_PATH}/inner_api/media_library_helper/include",
  ]
}

ohos_shared_library("media_library_camera_helper") {
  install_enable = true

  include_dirs = [
    "${MEDIALIB_BUSINESS_PATH}/media_analysis_data_manager/include/dao/table",
    "${MEDIALIB_BUSINESS_PATH}/media_camera_character_service/include/common_data",
    "${MEDIALIB_COMMON_PATH}/utils/include",
    "${MEDIALIB_INNERKITS_PATH}/media_library_helper/include",
    "${MEDIALIB_UTILS_PATH}/include",
  ]

  inner_ipc_include_dirs = [
    "${MEDIALIB_BUSINESS_PATH}/media_camera_character_service/include/vo",
    "${MEDIALIB_INNERKITS_PATH}/medialibrary_data_extension/include/ipc",
    "${MEDIALIB_INNERKITS_PATH}/medialibrary_data_extension/include/ipc/util",
    "${MEDIALIB_INNERKITS_PATH}/medialibrary_data_extension/include/ipc/vo",
  ]

  notify_include_dirs = [
    "${MEDIALIB_NEW_SERVICES_PATH}/media_notification/include/data",
    "${MEDIALIB_NEW_SERVICES_PATH}/media_refresh/include",
    "${MEDIALIB_INNERIMPL_PATH}/notification/include",
  ]

  include_dirs += inner_ipc_include_dirs
  include_dirs += notify_include_dirs

  sources = [
    "./src/media_library_camera_manager.cpp",
    "./src/media_low_quality_memory_callback.cpp",
    "./src/media_photo_asset_proxy.cpp",
    "${MEDIALIB_INNERKITS_PATH}/media_library_helper/src/base_column.cpp",
    "${MEDIALIB_INNERKITS_PATH}/media_library_helper/src/media_column.cpp",
    "${MEDIALIB_INNERKITS_PATH}/media_library_helper/src/mimetype_utils.cpp",
  ]

  inner_ipc_sources = [
    "${MEDIALIB_BUSINESS_PATH}/media_camera_character_service/src/vo/add_image_vo.cpp",
    "${MEDIALIB_BUSINESS_PATH}/media_camera_character_service/src/vo/add_process_video_vo.cpp",
    "${MEDIALIB_INNERKITS_PATH}/medialibrary_data_extension/src/ipc/util/user_inner_ipc_client.cpp",
  ]

  notify_sources = [
    "${MEDIALIB_INNERIMPL_PATH}/notification/src/medialibrary_notify_user_define_observer.cpp",
    "${MEDIALIB_NEW_SERVICES_PATH}/media_notification/src/data/user_define_notify_info.cpp",
  ]

  sources += inner_ipc_sources
  sources += notify_sources

  deps = [
    "${MEDIALIB_COMMON_PATH}:media_library_common",
  ]

  public_configs = [ ":media_library_camera_config" ]

  cflags = [
    "-fPIC",
    "-Wall",
    "-Werror",
    "-Wno-unused-but-set-variable",
    "-fdata-sections",
    "-ffunction-sections",
    "-Os",
  ]

  ldflags = [
    "-Wl,--gc-sections",
    "-fPIE",
  ]

  cflags_cc = [ "-Os" ]

  external_deps = [
    "ability_runtime:ability_connect_callback_stub",
    "ability_runtime:app_context",
    "bundle_framework:appexecfwk_core",
    "c_utils:utils",
    "data_share:datashare_consumer",
    "image_framework:image_native",
    "ipc:ipc_core",
    "player_framework:media_client",
    "relational_store:native_rdb",
  ]

  defines = []
  defines += [ "MEDIALIBRARY_COMPATIBILITY=1" ]

  if (!media_library_link_opt) {
    sanitize = {
      cfi = true
      cfi_cross_dso = true
      debug = false
      integer_overflow = true
      ubsan = true
      boundary_sanitize = true
      stack_protector_ret = true
    }
  }

  use_exceptions = true

  innerapi_tags = [ "platformsdk" ]
  part_name = "media_library"
  subsystem_name = "multimedia"
}