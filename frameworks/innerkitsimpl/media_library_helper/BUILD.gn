# Copyright (C) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/distributeddatamgr/data_share/datashare.gni")
import("//foundation/distributeddatamgr/relational_store/relational_store.gni")
import("//foundation/multimedia/media_library/media_library.gni")

group("media_library_packages") {
  deps = [ ":media_library" ]
}

config("media_library_config") {
  visibility = [ ":*" ]

  include_dirs = [
    "./",
    "./include",
    "//foundation/multimedia/media_library/interfaces/inner_api/media_library_helper/include",
    "//base/hiviewdfx/hilog/interfaces/native/innerkits/include",
    "//commonlibrary/c_utils/base/include",
    "${ABILITY_RUNTIME_PATH}/interfaces/kits/native/ability/native",
    "${relational_store_innerapi_path}/rdb/include",
    "${relational_store_innerapi_path}/appdatafwk/include",
    "//foundation/communication/ipc/interfaces/innerkits/ipc_core/include",
    "${ABILITY_RUNTIME_PATH}/interfaces/kits/native/ability/native",
    "${datashare_common_native_path}/include",
    "${datashare_innerapi_path}/common/include",
    "${datashare_innerapi_path}/consumer/include",
    "${datashare_innerapi_path}/provider/include",
    "${MEDIA_LIB_COMMON_UTILS_DIR}/include",
  ]

  cflags = [
    "-Wall",
    "-Werror",
  ]
}

ohos_source_set("media_library_source") {
  sources = [
    "src/album_asset.cpp",
    "src/dir_asset.cpp",
    "src/fetch_result.cpp",
    "src/file_asset.cpp",
    "src/media_asset.cpp",
    "src/media_file_utils.cpp",
    "src/media_volume.cpp",
    "src/native_album_asset.cpp",
    "src/smart_album_asset.cpp",
  ]

  configs = [ ":media_library_config" ]

  external_deps = [ "hitrace_native:hitrace_meter" ]

  part_name = "media_library"
  subsystem_name = "multimedia"
}

ohos_shared_library("media_library") {
  install_enable = true

  deps = [
    ":media_library_source",
    "${ABILITY_RUNTIME_PATH}/frameworks/native/ability/native:abilitykit_native",
  ]

  external_deps = [
    "c_utils:utils",
    "data_share:datashare_abilitykit",
    "data_share:datashare_common",
    "hitrace_native:hitrace_meter",
    "hiviewdfx_hilog_native:libhilog",
    "ipc:ipc_core",
    "relational_store:native_appdatafwk",
    "relational_store:native_dataability",
    "relational_store:native_rdb",
  ]

  part_name = "media_library"
  subsystem_name = "multimedia"
}

ohos_source_set("media_library_test_source") {
  sources = [ "//foundation/multimedia/media_library/interfaces/inner_api/media_library_helper/test/media_library_test.cpp" ]

  configs = [ ":media_library_config" ]

  part_name = "media_library"
  subsystem_name = "multimedia"
}

ohos_executable("media_library_test") {
  install_enable = true

  deps = [
    ":media_library",
    ":media_library_test_source",
    "//base/hiviewdfx/hilog/interfaces/native/innerkits:libhilog",
  ]

  external_deps = [ "relational_store:native_rdb" ]

  part_name = "media_library"
  subsystem_name = "multimedia"
}
ohos_static_library("media_thumbnail_helper") {
  sources = [ "src/media_thumbnail_helper.cpp" ]
  include_dirs = [
    "//foundation/multimedia/media_library/interfaces/inner_api/media_library_helper/include",
    "//foundation/multimedia/media_library/frameworks/innerkitsimpl/media_library_helper/include",
    "//foundation/distributeddatamgr/distributeddatamgr/interfaces/innerkits/distributeddata/include/",
    "//foundation/multimedia/image_standard/interfaces/innerkits/include/",
    "${ABILITY_RUNTIME_PATH}/interfaces/kits/native/ability/native",
    "//commonlibrary/c_utils/base/include",
    "${MEDIA_LIB_COMMON_UTILS_DIR}/include",
  ]
  deps = [
    "${ABILITY_RUNTIME_PATH}/frameworks/native/ability/native:abilitykit_native",
    "//foundation/distributeddatamgr/distributeddatamgr/interfaces/innerkits/distributeddata:distributeddata_inner",
    "//foundation/multimedia/image_standard/interfaces/innerkits:image_native",
  ]

  # cflags = [ "-DOLD_KV_API" ]
  external_deps = [
    "c_utils:utils",
    "data_share:datashare_abilitykit",
    "data_share:datashare_common",
    "hitrace_native:hitrace_meter",
    "hiviewdfx_hilog_native:libhilog",
    "relational_store:native_appdatafwk",
    "relational_store:native_dataability",
    "relational_store:native_rdb",
  ]
  subsystem_name = "multimedia"
  part_name = "media_library"
}

ohos_static_library("permission_utils") {
  sources = [ "src/permission_utils.cpp" ]
  include_dirs = [
    "./include",
    "$MEDIA_LIB_INNERKITS_DIR/include",
    "//foundation/multimedia/media_library/interfaces/inner_api/media_library_helper/include",

    # appexecfwk_core
    "//foundation/bundlemanager/bundle_framework/interfaces/inner_api/appexecfwk_core/include/bundlemgr",
    "//foundation/bundlemanager/bundle_framework/interfaces/inner_api/appexecfwk_base/include/",
    "//third_party/json/include",
    "//foundation/ability/ability_base/interfaces/kits/native/want/include",
    "//foundation/ability/ability_base/interfaces/kits/native/uri/include",
    "//foundation/ability/ability_base/interfaces/inner_api/base/include",
    "//foundation/ability/ability_runtime/services/abilitymgr/include",

    # Security stuff
    "//base/security/access_token/interfaces/innerkits/accesstoken/include",
    "//base/security/access_token/interfaces/innerkits/privacy/include",
  ]
  deps = [
    # IBundleMgr
    "${ABILITY_RUNTIME_SERVICES_PATH}/abilitymgr:abilityms",
    "//base/security/access_token/interfaces/innerkits/privacy:libprivacy_sdk",
    "//foundation/bundlemanager/bundle_framework/interfaces/inner_api/appexecfwk_core:appexecfwk_core",
  ]
  external_deps = [
    "access_token:libaccesstoken_sdk",
    "hiviewdfx_hilog_native:libhilog",
    "ipc:ipc_core",
    "samgr_standard:samgr_proxy",
  ]
}
